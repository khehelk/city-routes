plugins {
	id 'org.springframework.boot' version '3.3.5' apply false
	id 'io.spring.dependency-management' version '1.1.6' apply false
	id 'checkstyle'
}

subprojects {
	group = 'ru.khehelk.city-routes'
	version = '0.0.1-SNAPSHOT'

	def libraries = ['domain', 'business-logic', 'web']

	apply plugin: 'checkstyle'
	apply plugin: 'io.spring.dependency-management'

	if (libraries.contains(project.name)) {
		apply plugin: 'java-library'
		java {
			withSourcesJar()
		}
	} else {
		apply plugin: 'java'
		apply plugin: 'org.springframework.boot'
	}

	repositories {
		mavenCentral()
	}

	java {
		toolchain {
			languageVersion.set(JavaLanguageVersion.of(21))
		}
	}

	checkstyle {
		toolVersion = '10.5.0'
		configFile = file("$rootDir/_config/checkstyle/checkstyle.xml")
		configProperties = [
				'suppressionsFile': file("${rootProject.projectDir}/_config/checkstyle/suppressions.xml"),
				'xpathSuppressionsFile': file("${rootProject.projectDir}/_config/checkstyle/xpath-suppressions.xml")
		]
	}

	compileJava.options.encoding = 'UTF-8'

	test {
		useJUnitPlatform()
	}

	tasks.withType(JavaCompile).configureEach {
		options.encoding = 'UTF-8'
	}

	ext {
		set('spring_boot_version', '3.3.5')
		set('spring_cloud_version', '2023.0.3')
		set('mapstruct_version', '1.6.0')
		set('springdoc_version', '2.6.0')
		set('jjwt_version', '0.12.6')
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.boot:spring-boot-dependencies:${spring_boot_version}"
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${spring_cloud_version}"
		}
	}

	dependencies {
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	}
}